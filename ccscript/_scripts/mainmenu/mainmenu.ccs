import "../asm/cc_main_menu.ccs"
import "../asm/asm65816.ccs"
import "../../_scripts.ccs"
import "../modmenu/menus/modmenu.ccs"
import "../asm/JSL_with_RTS.ccs"
import "../modmenu/enums/windows.ccs"
import "../modmenu/enums/labels.ccs"
import "../modmenu/enums/flags.ccs"
import "../modmenu/commands.ccs"
import "../asm/movement_reloc.ccs"
import keyitems

ROM[0xc134af] = { MText(MainMenu) NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP ASM_JSL_With_RTS(BANKC1_RTL, 0xC10A1D) JMP(0xc13c8c) }

Selected_Goods:   { counter(1) eob }  Selected_Equip:    { counter(2) eob }
Selected_PSI:     { counter(3) eob }  Selected_KeyItems: { counter(4) eob }
Selected_ModMenu: { counter(5) eob }  Selected_Status:   { counter(6) eob }

MainMenu:
	//sprite_spawn(106, 896, 1) // Invisible camera sprite running "Cutscene On"
	// Stop player movement
	"[1F E5 FF]"
	// Open appropriately sized window
	window_closeall
	window_open(wnd_Action)
	Lbl(lbl_Action)
	ResetCounter()
	ctoarg
	// Options
	MenuOptionWithCallback("Goods", Selected_Goods)
	if flg_KeyItems { MenuOptionWithCallback("Key Items", Selected_KeyItems) }
	MenuOptionWithCallback("PSI", Selected_PSI)
	MenuOptionWithCallback("Equip", Selected_Equip)
	if flg_ModMenu { MenuOptionWithCallback("Mod Menu", Selected_ModMenu) }
	MenuOptionWithCallback("Status", Selected_Status)
	// Width
	DisplayVertical(2)
	// Exit by default
	if not "[11]" { goto(goto_end) }
	// Results
	ctoarg
    swap
	SwitchGoto(6)
        SwitchCase(goto_goods)
        SwitchCase(goto_equip)
		SwitchCase(goto_psi)
        SwitchCase(goto_keyitems)
		SwitchCase(goto_modmenu)
        SwitchCase(goto_status)
	swap
    "@Error! No option {number(0)} in this menu." goto(goto_end)
	goto_goods:
		window_closeall open_hp open_goods goto(MainMenu)
	goto_equip:
		window_closeall open_equip goto(MainMenu)
	goto_psi:
		window_closeall open_hp open_psi goto(MainMenu)
	goto_keyitems:
		window_closeall call(KeyItems) goto(MainMenu)
	goto_modmenu:
		window_closeall call(ModMenu_MainMenu) goto(MainMenu)
	goto_status:
		window_closeall open_status goto(MainMenu)
	goto_end:
		//sprite_spawn(106, 897, 1) // Invisible camera sprite running "Cutscene Off"
		// Resume player movement
		"[1F ED]"
eob