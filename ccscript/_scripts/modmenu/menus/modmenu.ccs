/* Mod Menu */
import "../enums/flags.ccs"
import "../enums/labels.ccs"
import "../enums/party.ccs"
import "../enums/windows.ccs"
import "../enums/counter.ccs"
import "../commands.ccs"
/* Submenus */
import "party/partymenu.ccs"
import "warp/warpmenu.ccs"
import "flags/flagsmenu.ccs"
import "battle/battlemenu.ccs"
import "goods/goodsmenu.ccs"
import "music/musicmenu.ccs"
import "map/palettemenu.ccs"
/* Commands */
import "party/health/healthcommands.ccs"
import "../../asm/cc_text_speed.ccs"
import "../../asm/movement_reloc.ccs"
import "../../asm/cc_main_menu.ccs"
import "../../asm/ccexpand.ccs"
import "../../asm/cc_asmcall.ccs"
import "../../asm/movement.ccs"
import "../../asm/asm65816_2.ccs"
import "../../asm/movscr_codes.ccs"

// Procedures

ModMenu: {
	// Close unnecessary windows
	CloseAllModMenuWnd()
	window_closeall
	// Open main Mod Menu Window
	OpenModMenuWnd()
	// Set label to "MOD MENU"
	Lbl(lbl_ModMenu)
	ResetCounter() ctoarg
	// Pick a menu option
	MenuOptionWithCallback("Party", Set_Count_1)
	MenuOptionWithCallback("Save", Set_Count_2)
	MenuOptionWithCallback("BGM", Set_Count_3)
	MenuOptionWithCallback("Goods", Set_Count_4)
	MenuOptionWithCallback("Warp", Set_Count_5)
	MenuOptionWithCallback("Battle", Set_Count_6)
	MenuOptionWithCallback("Flags", Set_Count_7)
	MenuOptionWithCallback("Map", Set_Count_8)
	MenuOptionWithCallback("Text Speed", Set_Count_9)
	DisplayVertical(1) if not "[11]" { eob }
	ctoarg swap
    SwitchGoto(9)
        SwitchCase(chose_party)
        SwitchCase(chose_save)
        SwitchCase(chose_bgm)
        SwitchCase(chose_goods)
		SwitchCase(chose_warp)
		SwitchCase(chose_battle)
		SwitchCase(chose_flags)
		SwitchCase(chose_map)
		SwitchCase(chose_textspeed)
    swap
	"@Error! No option {number(0)} in this menu." eob
	chose_party:
		goto(PartyMenu)
	chose_save:
		save SystemWnd(wnd_ModMenu, "@Game saved.") goto(ModMenu)
	chose_bgm:
		goto(MusicMenu)
	chose_goods:
		goto(GoodsMenu)
	chose_warp:
		clearline text_color(6) SetTextSpeedInstant() "Warp" sound(SND_CURSORCONFIRM) call(WarpMenu)
	chose_battle:
		goto(BattleMenu)
	chose_flags:
		goto(FlagsMenu)
	chose_map:
		goto(MapMenu)
	chose_textspeed:
		goto(TextSpeed)
}

ModMenu_AboutDlg:
	window_open(wnd_Dialog)
	Lbl(lbl_About)
	"@EarthBound Mod Menu by ShrineFox." newline
	"@Made using Coilsnake." newline
	"@Updated 3-17-22." wait
	window_close(wnd_Dialog)
goto(ModMenu)

MapMenu: {
	CloseModMenu2Wnd()
	OpenModMenu2Wnd()
	Lbl(lbl_Map)
	ResetCounter() ctoarg
	// Pick a menu option
	MenuOptionWithCallback("Palette", Set_Count_1)
	MenuOptionWithCallback("Corrupt", Set_Count_2)
	DisplayVertical(1) if not "[11]" { goto(ModMenu) }
	ctoarg swap
    SwitchGoto(2)
        SwitchCase(chose_palette)
        SwitchCase(chose_corrupt)
	swap
	"@Error! No option {number(0)} in this menu." goto(ModMenu)
	chose_palette:
		call(PaletteMenu) goto(MapMenu)
	chose_corrupt:
		"[1A 0F]" goto(MapMenu)
}

TextSpeed:
	ConfirmNumber(lbl_Test, 1, "Set this text speed?", SetTxtSpeed)
goto(ModMenu)

SetTxtSpeed:
	settextspeed(0)
	window_open(1)
	"T-e-s-t-i-n-g" next
	window_close(1)
eob