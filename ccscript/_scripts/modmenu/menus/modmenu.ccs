/* Mod Menu */
import "../enums/flags.ccs"
import "../enums/labels.ccs"
import "../enums/party.ccs"
import "../enums/windows.ccs"
import "../commands.ccs"
/* Submenus */
import "party/partymenu.ccs"
import "warp/warpmenu.ccs"
import "flags/flagsmenu.ccs"
import "battle/battlemenu.ccs"
import "goods/goodsmenu.ccs"
import "music/musicmenu.ccs"
/* Commands */
import "party/health/healthcommands.ccs"
import "../../asm/cc_text_speed.ccs"
import "../../asm/movement_reloc.ccs"
import "../../asm/cc_main_menu.ccs"
import "../../asm/ccexpand.ccs"
import "../../asm/cc_asmcall.ccs"
import "../../asm/movement.ccs"
import "../../asm/asm65816_2.ccs"
import "../../asm/movscr_codes.ccs"

// Procedures

ModMenu_MainMenu:
	call(ModMenu)
eob

ModMenu:
	// Close unnecessary windows
	CloseAllModMenuWnd()
	window_closeall
	// Open main Mod Menu Window
	OpenModMenuWnd()
	// Set label to "MOD MENU"
	Lbl(lbl_ModMenu)
	// Pick a menu option
	menu 1 {
		"Party": { call(PartyMenu) }
		"Save":  { save SystemWnd(wnd_ModMenu, "@Game saved.") }
		"BGM": 	 { call(MusicMenu) }
		"Warp":  { call(WarpMenu) }
		"Battle": { call(BattleMenu) }
		"Goods": { call(GoodsMenu) }
		"Flags": { call(FlagsMenu) }
		"Test":  { call(TestDlg)  }
		"Text Speed": { call(TextSpeed) }
		default "Exit": { goto(_end) }
	}
	goto(ModMenu)
	_end:
eob

ModMenu_AboutDlg:
	window_open(wnd_Dialog)
	Lbl(lbl_About)
	"@EarthBound Mod Menu by ShrineFox." newline
	"@Made using Coilsnake." newline
	"@Updated 3-6-22." wait
	window_close(wnd_Dialog)
goto(ModMenu_MainMenu)

command sprite_spawn(sprite,move,style) "[1F 15 {short sprite} {short move} {byte style}]"

TestDlg:
	sprite_spawn(106, 896, 1) // Invisible camera sprite running "Cutscene On"
	window_open(1)
	"@Testing letterbox..." next
	window_closeall
	sprite_spawn(106, 897, 1) // Invisible camera sprite running "Cutscene Off"
	m_destroy_self
eob

TextSpeed:
	ConfirmNumber(lbl_Test, 1, "Set this text speed?", SetTxtSpeed)
goto(ModMenu_MainMenu)

SetTxtSpeed:
	settextspeed(0)
	window_open(1)
	"T-e-s-t-i-n-g" next
	window_close(1)
eob