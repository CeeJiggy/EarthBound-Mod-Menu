/* Mod Menu */
import "../../enums/flags.ccs"
import "../../enums/labels.ccs"
import "../../enums/party.ccs"
import "../../enums/windows.ccs"
import "../../commands.ccs"

command WarpNotification(string) { SystemWnd(wnd_ModMenu, "{sound(SND_EDENWARP)}@Warped to {string}.") }

WarpMenu:
	CloseWindows()
	LblWnd(wnd_ModMenu2, lbl_Warp)
	menu 1 {
		"View List":    { goto(WarpList) }
		"Enter ID":     { goto(WarpID) }
		"Set Anchor":   { anchor_set SystemWnd(wnd_ModMenu2, "{sound(SND_BOXOPEN1)}@Anchor set to current location.") }
		"Go To Anchor": { anchor_warp WarpNotification("anchor location") }
		default "Back": { goto(modmenu.ModMenu_MainMenu) }
	}
goto(WarpMenu)

WarpList:
	CloseWindows()
	LblWnd(wnd_ModMenu, lbl_Warp)
		menu 1 {
		"Onett":         { warp(25)  WarpNotification("{teleportname(1)}") }
		"Twoson":        { warp(29)  WarpNotification("{teleportname(2)}") }
		"Threed":        { warp(15)  WarpNotification("{teleportname(3)}") }
		"Fourside":      { warp(20)  WarpNotification("{teleportname(5)}") }
		"Moonside":      { warp(24)  WarpNotification("Moonside") }
		"Winters":       { warp(30)  WarpNotification("{teleportname(6)}") }
		"Summers":       { warp(31)  WarpNotification("{teleportname(7)}") }
		"Saturn Valley": { warp(143) WarpNotification("{teleportname(4)}") }
		"Tenda Village": { warp(22)  WarpNotification("{teleportname(11)}") }
		"Magicant":      { warp(23)  WarpNotification("Magicant") }
		"Cave of Past":  { warp(26)  WarpNotification("Cave of the Past") }
		"Scaraba":       { warp(32)  WarpNotification("{teleportname(8)}") }
		"Dalaam":        { warp(27)  WarpNotification("{teleportname(9)}") }
		default "Back":  {  goto(WarpMenu) }
	}
goto(WarpList)

WarpID:
	CloseWindows()
	LblWnd(wnd_ModMenuConfirm, lbl_Warp)
	"@Enter ID:" newline
	input(3)
	"@Warp using ID {number(0)}?"
	menu 2
	{
		"Yes": { warp(0) WarpNotification("Warp ID {number(0)}") }
		default "No": { goto(WarpMenu) }
	}
goto(WarpID)